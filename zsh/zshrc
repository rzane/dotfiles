if [ ! -e ~/.antigen.zsh  ]; then
  echo "Installing antigen..."
  curl -L git.io/antigen > ~/.antigen.zsh
fi

source ~/.antigen.zsh

########################
# Packages
########################

antigen use oh-my-zsh
antigen bundle asdf
antigen bundle vi-mode
antigen bundle web-search
antigen bundle z
antigen bundle zsh-users/zsh-syntax-highlighting
antigen bundle zsh-users/zsh-history-substring-search
antigen bundle zsh-users/zsh-autosuggestions
antigen bundle mafredri/zsh-async
antigen bundle sindresorhus/pure
antigen apply

########################
# History search
########################

bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down
bindkey -M vicmd "k" history-substring-search-up
bindkey -M vicmd "j" history-substring-search-down
bindkey -M viins "$terminfo[kcuu1]" history-substring-search-up
bindkey -M viins "$terminfo[kcud1]" history-substring-search-down

########################
# Environment
########################

if [ $commands[nvim] ]; then
  export VISUAL=nvim
else
  export VISUAL=vim
fi

########################
# Shims
########################

if [ $commands[nvim] ]; then
  alias vim=nvim
fi

if [ $commands[bat] ]; then
  alias cat="bat --style plain"
fi

if [ $commands[exa] ]; then
  alias ls="exa --git --header"
  alias tree="exa --git --long --header --tree"
fi

########################
# Completion
########################

if [ $commands[kubectl] ]; then
  source <(kubectl completion zsh)
fi

########################
# FZF
########################

# Press ctrl-o in fzf to open a file in VIM
export FZF_DEFAULT_OPTS="--bind='ctrl-o:execute(vim {})+abort'"

# Search and preview files
alias preview="fzf --preview 'bat --color \"always\" {}'"

########################
# Git
########################

alias gs='git status'

function grb {
  git checkout "${1-master}"
  git pull origin "${1-master}"
  git checkout -
  git rebase "${1-master}"
}

function git-86 {
  git branch --merged master | egrep -v "(^\*|master)" | xargs git branch -d
}

########################
# Docker
########################

alias d='docker-compose'
alias dr='docker-compose run'

########################
# Ruby
########################

alias bi='bundle install'
alias be='bundle exec'

########################
# Utilities
########################

# What's the weather?
alias weather='curl wttr.in/philly'

# Find/replace all files in current directory
function gsub {
  if [ "$1" = "--commit" ]; then
    find . -type f -name "*.bak" -delete
  elif [ "$1" = "--revert" ]; then
    find . -type f -name "*.bak" -exec rename -f 's/\.bak$//' {} \;
  else
    ag -0 -l "$1" "${@:3}" | xargs -0 perl -pi.bak -e "s/$1/$2/g"
  fi
}

########################
# Services
########################

function service-up {
  if ! docker ps -a | awk '{print $NF}' | grep -q "^${1}$"; then
    docker create --name "$@"
  fi
  docker start "$1"
}

function service-down {
  docker stop "$1"
  docker rm "$1" --force
}

alias postgres-up='service-up postgres -p 5432:5432 postgres:alpine'
alias postgres-down='service-down postgres'

alias redis-up='service-up redis -p 6379:6379 redis:alpine'
alias redis-down='serivice-down redis'

########################
# Customization
########################

if [ -e ~/.zshrc.local ]; then
  source ~/.zshrc.local
fi
